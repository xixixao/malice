dump = require './jsDump'

module.exports = (OMeta, OMLib) -> analyze: (ast) ->
  error = console.error

  ometa MAliceSemantics
    trans = [:t apply(t):ans] -> ans

    Program createScope:scope trans*:declarations

    Function :name trans:returnType [trans*]:args createScope:scope {scope.addAll args} trans:body exitScope
      ->
        @declare new FunctionIdentifier name, returnType, args

    Argument :name :type
      ->
        new Reference name, type

    Block createScope:scope trans* exitScope

    Variable :name trans:type trans:value
      ->
        @declare new Reference name, type

    Array :name trans:size trans:elementType
      ->
        sizeValidType = @check size, [types.number]
        type = if sizeValidType then (new ArrayIdentifier elementType) else types.error
        @declare new Reference name, type

    Skip
      ->
    Assignment trans:assignable trans:value
      ->
        console.log "assignable: " + dump.parse assignable
        error "Wrong type in assignment" unless assignable.type.equals value.type

    Decrement trans:variable
      ->
        error "Wrong type" unless variable.type.equals types.number
    Increment trans:variable
      ->
        error "Wrong type" unless variable.type.equals types.number
    Output trans:value
      ->
        error "Arrays have no representation" if value.type.isArray?
    Call trans:fun trans*:args
      ->
        error "Trying to call what is not a function" unless fun.isFunction?
        error "Wrong number of arguments" if args.length isnt fun.args.length
        for arg, i in args
          error "Wrong type of an argument" unless arg.type.equals fun.args[i].type
    Return trans:value
      ->
        error "Arrays cannot be returned" if value.type.isArray?
    Input trans:variable
      ->
        error "Arrays cannot be input" if variable.type.isArray?
    If trans:cond [trans*] [trans*]
      ->
        error "Condition must be a boolean" unless cond.type.equals types.boolean
    Until trans:cond [trans*]
      ->
        error "Condition must be a boolean" unless cond.type.equals types.boolean

    Spider trans:itemType
      ->
        new ArrayIdentifier itemType

    Type :label
      ->
        new Type label

    Logic :op trans:b1 trans:b2
      ->
        error "Wrong type of operand, expected boolean" unless b1.type.equals types.boolean
        error "Wrong type of operand, expected boolean" unless b2.type.equals types.boolean
        new Value types.boolean

    Compare :op trans:e1 trans:e2
      ->
        error "Wrong type of operand, expected number or a letter" unless (e1.type.equalsAny types.number, types.letter)
        new Value types.boolean

    Arithmetic :op trans:e1 trans:e2
      ->
        error "Wrong type of operand, expected number" unless e1.type.equals types.number
        error "Wrong type of operand, expected number" unless e2.type.equals types.number
        new Value types.number

    Dereference :name
      ->
        @lookup name

    Element trans:array trans:index
      ->
        if not array.type.isArray
          error "Trying to access element of nonarray"
          type = types.error
        else
          type = array.type.itemType
        error "Array's index must be a number" unless index.type.equals types.number
        new Value type

    Number :n
      ->
        new Value types.number

    Letter :ch
      ->
        new Value types.number

    Sentece :string
      ->
        new Value types.sentence

    createScope
      ->
        outerScope = @currentScope
        @currentScope = new Scope outerScope, {}

    exitScope
      ->
        console.log @currentScope
        @currentScope = @currentScope.outer


  MAliceSemantics::declare = (identifier) ->
    alreadyDeclared = @currentScope.declaredInCurrent identifier
    if alreadyDeclared
      console.error "#{identifier.name} already declared in current scope"
    else
      @currentScope.add identifier
    console.log "#{identifier.name} declared"
    identifier

  MAliceSemantics::lookup = (name) ->
    @lookupIn name, @currentScope

  MAliceSemantics::lookupIn = (name, scope) ->
    reference = scope.get name
    if reference?
      return reference
    else
      console.error "#{name} undeclared"
      return new Reference name, types.error

  class Node
    constructor: (@type) ->

  class Scope
    constructor: (@outer, @declarations) ->

    toString: ->
      "Scope#{dump.parse @declarations}"

    add: (identifier) ->
      @declarations[identifier.name] = identifier

    addAll: (identifiers) ->
      @add identifier for identifier in identifiers

    declaredInCurrent: (identifier) ->
      @declarations[identifier.name]?

    get: (name) ->
      @declarations[name] or @outer?.get name

  class Identifier # Could also hold a reference to the ASTnode that declares Identifier

  class Type extends Identifier
    constructor: (@label) ->
    equals: (t) ->
      @label is t.label or t.isError
    equalsAny: (ts...) ->
      ts.some (t) -> @equals t

  class ErrorType extends Identifier
    isError: yes
    equals: (t) ->
      yes

  class Value extends Identifier
    constructor: (@type) ->

  class Reference extends Identifier
    constructor: (@name, @type) ->

  class FunctionIdentifier extends Identifier
    constructor: (@name, @returnType, @args, @scope) ->
    isFunction: yes

  class ArrayIdentifier extends Type
    constructor: (@itemType) ->
    isArray: yes
    equals: (t) ->
      @.itemType.equals t.itemType

  types =
    number: new Type "number"
    letter: new Type "letter"
    sentence: new Type "sentence"
    boolean: new Type "boolean"
    error: new ErrorType

  MAliceSemantics.match ast, "trans", undefined, (m, i) -> error "ERROR in #{m}, #{i}"